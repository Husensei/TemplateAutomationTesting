plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

wrapper {
    gradleVersion = '8.7'
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
}

group = 'dev.husensei'
version = '1.0-SNAPSHOT'

ext {
    junitVersion = "5.12.1"
    cucumberVersion = "7.21.1"
    seleniumVersion = "4.32.0"
    restAssuredVersion = "5.5.1"
    aspectJVersion = "1.9.23"
    allureVersion = "2.29.1"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
    options.compilerArgs.add('-Xlint:deprecation')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }

    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    // âœ… Use BOM to align JUnit Platform modules
    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.platform:junit-platform-suite'
    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.vintage:junit-vintage-engine'

    // Cucumber JUnit 5 support
    testImplementation platform("io.cucumber:cucumber-bom:$cucumberVersion")
    testImplementation 'io.cucumber:cucumber-java'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine'

    // Selenium
    testImplementation platform("org.seleniumhq.selenium:selenium-bom:$seleniumVersion")
    testImplementation 'org.seleniumhq.selenium:selenium-java'
    testImplementation 'org.seleniumhq.selenium:selenium-devtools-v135'

    // WebDriverManager
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.9.3'

    // REST Assured & JSON
    testImplementation platform("io.rest-assured:rest-assured-bom:$restAssuredVersion")
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.rest-assured:json-path'
    testImplementation 'io.rest-assured:json-schema-validator'
    testImplementation 'org.json:json:20250107'

    // Reporting
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation 'io.qameta.allure:allure-cucumber7-jvm'
    testImplementation "io.qameta.allure:allure-junit-platform"
    testImplementation 'io.qameta.allure:allure-junit5'

    // Logging
    testImplementation 'ch.qos.logback:logback-classic:1.5.18'
    testImplementation 'org.slf4j:slf4j-api:2.0.17'

    // BrowserMob Proxy
    testImplementation 'net.lightbody.bmp:browsermob-core:2.1.5'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    systemProperties System.getProperties().findAll { key, _ ->
        key in ["cucumber.filter.tags", "browser", "headless", "remote", "grid.url", "env"]
    }
    systemProperty "env", System.getProperty("env", "dev")

    ignoreFailures = true

    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]

    maxParallelForks = Runtime.runtime.availableProcessors()
}