plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

wrapper {
    gradleVersion = '8.7'
}

repositories {
    mavenCentral()
}

group = 'dev.husensei'
version = '1.0-SNAPSHOT'

def junitVersion = "5.12.1"
def cucumberVersion = "7.21.1"
def seleniumVersion = "4.31.0"
def aspectJVersion = '1.9.23'
def allureVersion = "2.29.1"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
    options.compilerArgs.add('-Xlint:deprecation')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }

    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    // âœ… Use BOM to align JUnit Platform modules
    testImplementation platform("org.junit:junit-bom:$junitVersion")

    // JUnit Jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // JUnit Platform
    testImplementation 'org.junit.platform:junit-platform-suite'
    testImplementation 'org.junit.platform:junit-platform-launcher'

    // JUnit Vintage
    testImplementation 'org.junit.vintage:junit-vintage-engine'

    // Cucumber JUnit 5 support
    testImplementation platform("io.cucumber:cucumber-bom:$cucumberVersion")
    testImplementation 'io.cucumber:cucumber-java'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine'

    // Selenium
    testImplementation platform("org.seleniumhq.selenium:selenium-bom:$seleniumVersion")
    testImplementation 'org.seleniumhq.selenium:selenium-java'
    testImplementation 'org.seleniumhq.selenium:selenium-devtools-v135'

    // WebDriverManager
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.9.3'

    // REST Assured & JSON
    testImplementation 'io.rest-assured:rest-assured:5.5.1'
    testImplementation 'io.rest-assured:json-path:5.5.1'
    testImplementation 'org.json:json:20250107'

    // Reporting
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation 'io.qameta.allure:allure-cucumber7-jvm'
    testImplementation "io.qameta.allure:allure-junit-platform"
    testImplementation 'io.qameta.allure:allure-junit5'

    // Logging
    testImplementation 'ch.qos.logback:logback-classic:1.5.18'
    testImplementation 'org.slf4j:slf4j-api:2.0.17'

    // BrowserMob Proxy
    testImplementation 'net.lightbody.bmp:browsermob-core:2.1.5'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
    systemProperty "browser", System.getProperty("browser")
    systemProperty "headless", System.getProperty("headless")
    systemProperty "remote", System.getProperty("remote")
    systemProperty "grid.url", System.getProperty("grid.url")
    systemProperty "env", System.getProperty("env", "dev")

    ignoreFailures = true

    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]

    maxParallelForks = Runtime.runtime.availableProcessors()
}